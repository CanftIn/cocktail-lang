%code top {
#include <algorithm>
#include <cstdarg>
#include <cstdio>
#include <cstdlib>
#include <iostream>
#include <list>

#include "experimental/SyntaxHelper.h"
}

%code requires {
#include "experimental/AST/Declaration.h"
#include "experimental/AST/ExpressionOrFieldList.h"
#include "experimental/AST/FunctionDefinition.h"
}

%code {
extern int yylineno;
extern int yylex();

void yyerror(char* error) {
  Cocktail::PrintSyntaxError(error, yylineno);
}
// void yyerror(char* error, ...);
}

%union {
  char* str;
  int num;
  Cocktail::Expression* expression;
  std::list<std::pair<std::string, Cocktail::Expression*>>* field_types;
  Cocktail::Statement* statement;
  Cocktail::Statement* statement_list;
  Cocktail::FunctionDefinition* function_definition;
  Cocktail::Declaration* declaration;
  std::list<Cocktail::Declaration*>* declaration_list;
  Cocktail::Member* member;
  std::list<Cocktail::Member*>* member_list;
  Cocktail::ExpOrFieldList* field_list;
  std::pair<std::string, Cocktail::Expression*>* alternative;
  std::list<std::pair<std::string, Cocktail::Expression*>>* alternative_list;
  std::pair<Cocktail::Expression*, Cocktail::Statement*>* clause;
  std::list<std::pair<Cocktail::Expression*, Cocktail::Statement*>>* clause_list;
  Cocktail::Expression* fun_type;
};

%token <num> integer_literal
%token <str> identifier
%type <str> designator
%type <declaration> declaration
%type <function_definition> function_declaration
%type <function_definition> function_definition
%type <declaration_list> declaration_list
%type <statement> statement
%type <statement> optional_else
%type <statement_list> statement_list
%type <expression> expression
%type <expression> pattern
%type <expression> return_type
%type <expression> tuple
%type <member> member
%type <member_list> member_list
%type <field_list> field
%type <field_list> field_list
%type <alternative> alternative
%type <alternative_list> alternative_list
%type <clause> clause
%type <clause_list> clause_list
%token AND
%token OR
%token NOT
%token INT
%token BOOL
%token TYPE
%token FN
%token FNTY
%token ARROW
%token VAR
%token EQUAL
%token IF
%token ELSE
%token WHILE
%token BREAK
%token CONTINUE
%token RETURN
%token TRUE
%token FALSE
%token STRUCT
%token CHOICE
%token MATCH
%token CASE
%token DBLARROW
%token DEFAULT
%token AUTO
%nonassoc '{' '}'
%nonassoc ':' ',' DBLARROW
%left OR AND
%nonassoc EQUAL NOT
%left '+' '-'
%left '.' ARROW
%nonassoc '(' ')' '[' ']'
%start input
%locations
%%
input: declaration_list
    { Cocktail::ExecProgram($1); }
;
pattern:
  expression
    { $$ = $1; }
;
expression:
  identifier
    { $$ = Cocktail::MakeVar(yylineno, $1); }
| expression designator
    { $$ = Cocktail::MakeGetField(yylineno, $1, $2); }
| expression '[' expression ']'
    { $$ = Cocktail::MakeIndex(yylineno, $1, $3); }
| expression ':' identifier
    { $$ = Cocktail::MakeVarPat(yylineno, $3, $1); }
| integer_literal
    { $$ = Cocktail::MakeInt(yylineno, $1); }
| TRUE
    { $$ = Cocktail::MakeBool(yylineno, true); }
| FALSE
    { $$ = Cocktail::MakeBool(yylineno, false); }
| INT
    { $$ = Cocktail::MakeIntType(yylineno); }
| BOOL
    { $$ = Cocktail::MakeBoolType(yylineno); }
| TYPE
    { $$ = Cocktail::MakeTypeType(yylineno); }
| AUTO
    { $$ = Cocktail::MakeAutoType(yylineno); }
| tuple { $$ = $1; }
| expression EQUAL expression
    { $$ = Cocktail::MakeBinOp(yylineno, Cocktail::Operator::Eq, $1, $3); }
| expression '+' expression
    { $$ = Cocktail::MakeBinOp(yylineno, Cocktail::Operator::Add, $1, $3); }
| expression '-' expression
    { $$ = Cocktail::MakeBinOp(yylineno, Cocktail::Operator::Sub, $1, $3); }
| expression AND expression
    { $$ = Cocktail::MakeBinOp(yylineno, Cocktail::Operator::And, $1, $3); }
| expression OR expression
    { $$ = Cocktail::MakeBinOp(yylineno, Cocktail::Operator::Or, $1, $3); }
| NOT expression
    { $$ = Cocktail::MakeUnOp(yylineno, Cocktail::Operator::Not, $2); }
| '-' expression
    { $$ = Cocktail::MakeUnOp(yylineno, Cocktail::Operator::Neg, $2); }
| expression tuple
    {
      if ($2->tag == Cocktail::ExpressionKind::Tuple) {
        $$ = Cocktail::MakeCall(yylineno, $1, $2);
      } else {
        auto vec =
            new std::vector<std::pair<std::string, Cocktail::Expression*>>();
        vec->push_back(std::make_pair("", $2));
        $$ = Cocktail::MakeCall(yylineno, $1, Cocktail::MakeTuple(yylineno, vec));
      }
    }
| FNTY tuple return_type
    { $$ = Cocktail::MakeFunType(yylineno, $2, $3); }
;
designator: '.' identifier { $$ = $2; }
;
tuple: '(' field_list ')'
    {
      switch ($2->tag) {
      case Cocktail::ExpOrFieldListKind::Exp:
        $$ = $2->u.exp;
        break;
      case Cocktail::ExpOrFieldListKind::FieldList:
        auto vec = new std::vector<std::pair<std::string,Cocktail::Expression*>>(
            $2->u.fields->begin(), $2->u.fields->end());
        $$ = Cocktail::MakeTuple(yylineno, vec);
        break;
      }
    }
;
field:
  pattern
    { $$ = Cocktail::MakeExp($1); }
| designator '=' pattern
    {
      auto fields =
          new std::list<std::pair<std::string, Cocktail::Expression*>>();
      fields->push_back(std::make_pair($1, $3));
      $$ = Cocktail::MakeFieldList(fields);
    }
;
field_list:
  // Empty
    {
      $$ = Cocktail::MakeFieldList(
          new std::list<std::pair<std::string, Cocktail::Expression*>>());
    }
| field
    { $$ = $1; }
| field ',' field_list
    { $$ = Cocktail::MakeConsField($1, $3); }
;
clause:
  CASE pattern DBLARROW statement
    { $$ = new std::pair<Cocktail::Expression*, Cocktail::Statement*>($2, $4); }
| DEFAULT DBLARROW statement
    {
      auto vp = Cocktail::MakeVarPat(yylineno, "_",
                                   Cocktail::MakeAutoType(yylineno));
      $$ = new std::pair<Cocktail::Expression*, Cocktail::Statement*>(vp, $3);
    }
;
clause_list:
  // Empty
    {
      $$ = new std::list<std::pair<Cocktail::Expression*, Cocktail::Statement*>>();
    }
| clause clause_list
    { $$ = $2; $$->push_front(*$1); }
;
statement:
  expression '=' expression ';'
    { $$ = Cocktail::MakeAssign(yylineno, $1, $3); }
| VAR pattern '=' expression ';'
    { $$ = Cocktail::MakeVarDef(yylineno, $2, $4); }
| expression ';'
    { $$ = Cocktail::MakeExpStmt(yylineno, $1); }
| IF '(' expression ')' statement optional_else
    { $$ = Cocktail::MakeIf(yylineno, $3, $5, $6); }
| WHILE '(' expression ')' statement
    { $$ = Cocktail::MakeWhile(yylineno, $3, $5); }
| BREAK ';'
    { $$ = Cocktail::MakeBreak(yylineno); }
| CONTINUE ';'
    { $$ = Cocktail::MakeContinue(yylineno); }
| RETURN expression ';'
    { $$ = Cocktail::MakeReturn(yylineno, $2); }
| '{' statement_list '}'
    { $$ = Cocktail::MakeBlock(yylineno, $2); }
| MATCH '(' expression ')' '{' clause_list '}'
    { $$ = Cocktail::MakeMatch(yylineno, $3, $6); }
;
optional_else:
  // Empty
    { $$ = 0; }
| ELSE statement { $$ = $2; }
;
statement_list:
  // Empty
    { $$ = 0; }
| statement statement_list
    { $$ = Cocktail::MakeSeq(yylineno, $1, $2); }
;
return_type:
  // Empty
    {
      $$ = Cocktail::MakeTuple(
          yylineno,
          new std::vector<std::pair<std::string, Cocktail::Expression*>>());
    }
| ARROW expression
    { $$ = $2; }
;
function_definition:
  FN identifier tuple return_type '{' statement_list '}'
    { $$ = MakeFunDef(yylineno, $2, $4, $3, $6); }
| FN identifier tuple DBLARROW expression ';'
    {
      $$ = Cocktail::MakeFunDef(yylineno, $2, Cocktail::MakeAutoType(yylineno), $3,
                              Cocktail::MakeReturn(yylineno, $5));
    }
;
function_declaration:
  FN identifier tuple return_type ';'
    { $$ = MakeFunDef(yylineno, $2, $4, $3, 0); }
;
member:
  VAR expression ':' identifier ';'
    { $$ = MakeField(yylineno, $4, $2); }
;
member_list:
  // Empty
    { $$ = new std::list<Cocktail::Member*>(); }
| member member_list
    { $$ = $2; $$->push_front($1); }
;
alternative:
  identifier tuple ';'
    { $$ = new std::pair<std::string, Cocktail::Expression*>($1, $2); }
;
alternative_list:
  // Empty
    { $$ = new std::list<std::pair<std::string, Cocktail::Expression*>>(); }
| alternative alternative_list
    { $$ = $2; $$->push_front(*$1); }
;
declaration:
  function_definition
    { $$ = Cocktail::MakeFunDecl($1); }
| function_declaration
    { $$ = Cocktail::MakeFunDecl($1); }
| STRUCT identifier '{' member_list '}'
    { $$ = Cocktail::MakeStructDecl(yylineno, $2, $4); }
| CHOICE identifier '{' alternative_list '}'
    { $$ = Cocktail::MakeChoiceDecl(yylineno, $2, $4); }
;
declaration_list:
  // Empty
    { $$ = new std::list<Cocktail::Declaration*>(); }
| declaration declaration_list
    {
      $$ = $2;
      $$->push_front($1);
    }
;
%%